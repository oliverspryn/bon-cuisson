<?xml version="1.0" encoding="utf-8"?>
<s:ItemRenderer xmlns:fx="http://ns.adobe.com/mxml/2009" 
				xmlns:s="library://ns.adobe.com/flex/spark" 
				xmlns:mx="library://ns.adobe.com/flex/mx" 
				xmlns:images="assets.images.*"
				autoDrawBackground="false" minWidth="800" width="100%"
				dataChange="init(event)">
	<fx:Script>
		<![CDATA[
			import flashx.textLayout.conversion.TextConverter;
			
			import mx.controls.Alert;
			import mx.events.FlexEvent;
			
		/**
		 * Initialization performs the following actions:
		 *  - adds a ":" after the name, if a tagline exists
		 *  - assign the <s:Image> a source, if there is one, so that a broken image icon isn't shown for
		 *    a blank image source
		 *  - either format the price using the CurrencyFormatter class, or construct a list of variations
		 *    to show various prices for differ sizes of menu items
		*/
			private function init(e:Event):void {
			//Add a ":" after the name if a tagline exists
				if (data.tagline != null && data.tagline != "") {
					title.text += " : ";
				}
				
			//Assign the <s:Image> a source
				if (data.imageURL != null && data.imageURL != "") {
					image.source = data.imageURL;
				}
				
			//Format the price or price variations
				if (data.variations == null || (data.variations != null && data.variations == "")) {
					price.text = priceFormatter.format(data.price);
					price.text += data.perUnit;
				} else {
					var variations:Object = JSON.parse(data.variations);
					var returnString:String = "";
					
					/**
					 * We can navigate the above object like a multi-dimensional array:
					 *  - first object within the array is the price
					 *  - second object within the array is the size
					 *  - third object within the array is the suggested serving audience
				    */
					
					for (var i:int = 0; i <= variations.length - 1; i++) {
						returnString += priceFormatter.format(variations[i][0]) + " " + variations[i][1] +
										"<br><font size=\"10\">" + "" + variations[i][2] + "</font><br>";
					}
					
					this.price.textFlow = TextConverter.importToFlow(returnString, TextConverter.TEXT_FIELD_HTML_FORMAT);
				}
			}
			
		]]>
	</fx:Script>
	
	<fx:Declarations>
		<s:CurrencyFormatter currencySymbol="$ " fractionalDigits="0" id="priceFormatter" trailingZeros="false" useCurrencySymbol="true" useGrouping="true"/>
	</fx:Declarations>
	
<!-- Include the required stylesheet -->
	<fx:Style source="../assets/styles/pages/Menu.css"/>
	
<!-- Display the header of the menu item including the name, price, tagline, and Fleur de Lis as the "bullet" item -->
	<s:RichText id="price" left="5" styleName="price" top="7"/>
	<images:FleurDeLis left="{width * 0.1}" />
	<s:Label id="title" left="{(width * 0.1) + 35}" styleName="name" text="{data.name}"/>
	<s:Label id="tagline" left="{title.x + title.width + 10}" right="0" styleName="tag" text="{data.tagline}" top="6"/>
	
<!-- Display the description and associated image (if any) of the menu item -->
	<s:Image id="image" left="{(width * 0.1) + 35}" top="{tagline.height + 35}" maxWidth="300"/>
	<s:Label left="{image.x + image.width + 15}" right="0" top="{tagline.height + 35}" styleName="description" text="{data.description}"/>
</s:ItemRenderer>