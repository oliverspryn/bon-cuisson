<?xml version="1.0" encoding="utf-8"?>
<s:Application xmlns:fx="http://ns.adobe.com/mxml/2009"
			   xmlns:s="library://ns.adobe.com/flex/spark"
			   xmlns:mx="library://ns.adobe.com/flex/mx"
			   xmlns:header="components.header.*"
			   xmlns:pages="components.pages.*"
			   backgroundColor="0x141414" minHeight="600" minWidth="955" skinClass="skins.Theme">
	<fx:Script>
		<![CDATA[
			import com.asual.swfaddress.SWFAddress;
			import com.asual.swfaddress.SWFAddressEvent;
			import com.forwardfour.Config;
			import com.forwardfour.events.MenuEvent;
			import com.forwardfour.events.NetworkEvent;
			import com.forwardfour.utils.ModuleBase;
			
			import mx.controls.Alert;
			
		//Globalize a reference to the page content
			private var pageObject:ModuleBase = null;
			
		//Globalize a reference to the old page content
			private var oldPageObject:ModuleBase = null;
			
		//Instantiate the configuration class
			public var config:Config = new Config();
			
		/**
		 * Initialization
		 * -------------------------------
		*/
			
		//Initialize SWFAddress and grab the page listed from the URL
			private function init(e:Event):void {
				//SWFAddress.addEventListener(SWFAddressEvent.INIT, URLNavigateToPageHandler);
				SWFAddress.addEventListener(SWFAddressEvent.CHANGE, URLNavigateToPageHandler);
			}
			
		//Get the hijaxed URL of the current page
			private function get URL():String {
				var URL:String = SWFAddress.getValue();
				
				if (URL == "" || URL == "/") {
					return "";
				} else {
					return URL.substring(1);
				}
			}
			
		/**
		 * Navigation handlers
		 * -------------------------------
		*/
			
		//Go to a specific page when a menu item has been clicked
			private function menuNavigateToPageHandler(e:MenuEvent):void {
				this.navigateToPage(menu.getInfoForItemByIndex());
			}
			
		//Request a page when the URL has changed
			private function URLNavigateToPageHandler(e:SWFAddressEvent):void {
				this.navigateToPage(menu.getInfoForItemByURL(this.URL));
			}
			
		//Process the page navigation request
			private function navigateToPage(pageData:Object):void {
				var pageType:String = pageData.pageType;
				
				switch(pageType) {
					case "home" : 
						homePage.load("services/home.php");
						homePage.addEventListener("loadComplete", transitionPages);
						this.oldPageObject = this.pageObject;
						this.pageObject = homePage;
						break;
					
					case "menu" : 
						menuPage.load("services/food-menu.php?type=" + pageData.category);
						menuPage.addEventListener("loadComplete", transitionPages);
						this.oldPageObject = this.pageObject;
						this.pageObject = menuPage;
					
					default : 
						break;
				}
			}
			
		//Transition the old page away
			private function transitionPages(e:NetworkEvent):void {
				this.pageObject.removeEventListener("loadComplete", transitionPages);
				
				if (this.oldPageObject != null) {
					this.oldPageObject.transitionOut();
					this.oldPageObject.addEventListener("transitionOutComplete", finishTransition);
				} else {
					this.finishTransition(new Event("transitionOutComplete"))
				}
			}
			
		//...and the new one into view
			private function finishTransition(e:Event):void {
				if (this.oldPageObject != null) {
					this.oldPageObject.removeEventListener("transitionOutComplete", finishTransition);
					this.oldPageObject = null;
				}
				
				var info:Object = menu.getInfoForItemByIndex();
				SWFAddress.setTitle(info.pageTitle);
				//SWFAddress.setValue(info.pageURL);
				this.pageObject.transitionIn();
			}
		]]>
	</fx:Script>
	
<!-- Include the navigation menu -->
	<header:Menu id="menu" top="0" menuConstructed="init(event)" menuItemClicked="menuNavigateToPageHandler(event)"/>
	
<!-- We'll use these components as we need them -->
	<s:Group width="100%" top="60">
		<pages:Home alpha="0" id="homePage" y="20"/>
		<pages:FoodMenu alpha="0" id="menuPage" y="20"/>
		<pages:Lunches id="lunchesPage" alpha="0" y="20" visible="false"/>
		<pages:Reviews id="reviewsPage" alpha="0" y="20" visible="false"/>
	</s:Group>
</s:Application>