<?xml version="1.0" encoding="utf-8"?>
<s:Group xmlns:fx="http://ns.adobe.com/mxml/2009" 
		 xmlns:s="library://ns.adobe.com/flex/spark" 
		 xmlns:mx="library://ns.adobe.com/flex/mx"
		 width="100%"
		 creationComplete="init(event)">
	<fx:Script>
		<![CDATA[			
			import com.asual.swfaddress.SWFAddress;
			import com.asual.swfaddress.SWFAddressEvent;
			import com.forwardfour.events.MenuEvent;
			import com.forwardfour.exceptions.ServerDataGenerationError;
			import com.forwardfour.valueObjects.NavigationItemData;
			import com.greensock.TweenMax;
			
			import mx.controls.Alert;
			import mx.core.FlexGlobals;
			import mx.core.IVisualElement;
			import mx.core.UIComponent;
			import mx.events.FlexEvent;
			import mx.rpc.events.FaultEvent;
			import mx.rpc.events.ResultEvent;
			
			import spark.events.IndexChangeEvent;
			
		//Globalize a reference to the main application, so that the instantiated Config class can be accessed
			private var main:Main;
			
		//A reference to the data that was fetched from the server
			[Bindable]
			private var data:Object;
			
		//A reference to the current page pointer arrow
			private var pointer:UIComponent = null;
			
		//Hide the menu scrollbars, configure this component, and initialize SWFAddress
			private function init(e:Event):void {	
				list.scroller.setStyle("horizontalScrollPolicy", "off");
				list.scroller.setStyle("verticalScrollPolicy", "off");
				
				this.configure();
				
				SWFAddress.addEventListener(SWFAddressEvent.INIT, request);
			}
			
		//Use the configuration class to set the menu's construction properties and behaviors
			private function configure():void {
				this.main = Main(FlexGlobals.topLevelApplication);
				this.dataService.showBusyCursor = this.main.config.showFlexBusyCursor;
			}
			
		//Request data from the server, now that we can access the URL
			private function request(e:SWFAddressEvent):void {
				dataServiceResponder.token = dataService.send();
			}
			
		//Share the loaded data with the component and transition the menu into view
			private function dataResultHandler(e:ResultEvent):void {
			//Share the data with the application
				try {
					if (e.result.menu && e.result.menu != null) {
						this.data = e.result.menu;
					} else {
						throw new ServerDataGenerationError("The server has returned invalid data while generating data for the Menu");
					}
				} catch (error:ServerDataGenerationError) {
					Alert.show(error.message);
				}
				
			//Transition the menu into view
				this.slideOut();
				
			//Dispatch a data fetched event
				super.dispatchEvent(new Event("dataFetched"));
				
				if (list.selectedIndex == -1) {
					list.addEventListener(FlexEvent.VALUE_COMMIT, menuConstructedHandler);
				} else {
					this.menuConstructedHandler(new FlexEvent("menuConstructed"));
				}
			}
			
		//Select the appropriate menu item just before construction complete, then dispatch a construction completed event
			private function menuConstructedHandler(e:FlexEvent):void {
				var menuData:Object = getInfoForItemByURL(this.URL);
				this.list.selectedIndex = menuData.menuIndex;
				
				super.dispatchEvent(new Event("menuConstructed"));
			}
			
		//Handle data loading errors
			private function dataFaultHandler(e:FaultEvent):void {
				Alert.show("Data for the Menu could not be fetched.\n\nFault code: " +
							e.fault.faultCode + "\nFault details: " + e.fault.faultDetail, e.fault.faultString);
			}
			
		/**
		 * Perform the following actions to transitioning the menu into view
		 *  - [1] slide the out horizontally to full width
		 *  - [2] roll the menu down to full height
		 *  - [3] build the menu items (performed by each item renderer)
		 *  - [4] build the current page pointer arrow
		*/
			
			private function slideOut():void {
				TweenMax.to(this.background, (this.main.config.menuInitDuration / 1000) / 2, {
					onComplete : slideDown,
					width : stage.stageWidth
				});
			}
			
			private function slideDown():void {
				TweenMax.to(this.background, (this.main.config.menuInitDuration / 1000) / 2, {
					height : 40,
					onComplete : buildPointerDelay
				});
				
			//Anchor the backgroud of the menu to the left and right of the stage
				this.background.left = 0;
				this.background.right = -20;
			}
			
			private function buildPointerDelay():void {
				var delay:Number = this.main.config.menuInitDuration;                //Delay for the menu background
				delay += this.data.item.length * this.main.config.menuItemDuration;  //Delay for each menu item
				
				var timer:Timer = new Timer(delay, 1);
				timer.addEventListener(TimerEvent.TIMER_COMPLETE, buildPointer);
				timer.start();
			}
			
			private function buildPointer(e:TimerEvent):void {
			//Pointer configuration
				var color:uint = 0xFFFFFF;
				var width:int = this.main.config.menuPointerSize;
				
			//Building the pointer
				this.pointer = new UIComponent();
				this.pointer.alpha = 0;
				this.pointer.graphics.beginFill(color);
				this.pointer.graphics.moveTo(width / 2, 0);
				this.pointer.graphics.lineTo(width, width);
				this.pointer.graphics.lineTo(0, width);
				this.pointer.graphics.lineTo(width / 2, 0);
				this.pointer.graphics.endFill();
				this.pointer.y = 40 - width;
				this.addElement(this.pointer);
				
			//Place the arrow under the active menu item
				this.pointer.x = this.getArrowPositionAtIndex(this.selected);
				this.selected = this.selected;
				
			//Dispatch a render complete event
				super.dispatchEvent(new Event("renderComplete"));
			}
			
		/**
		 * Getters and setters
		 * -------------------------------
		*/
			
		//Get the hijacked URL of the current page
			private function get URL():String {
				var URL:String = SWFAddress.getValue();
				
				if (URL == "" || URL == "/") {
					return "";
				} else {
					return URL.substring(1);
				}
			}
			
		//Get the selected menu item
			public function get selected():Number {
				return list.selectedIndex;
			}
			
		//Set the selected menu item and slide the arrow to a new menu item
			public function set selected(index:Number):void {
			//Don't do anything if the pointer has not be created
				if (this.pointer != null) {
					list.selectedIndex = index;
					
					var xPos:Number = this.getArrowPositionAtIndex(this.selected);
					
					TweenMax.to(this.pointer, this.main.config.menuPointerTransitionDuration / 1000, {
						alpha : 1,
						x : xPos
					});
				}
			}
			
		//Get the x-axis position of the pointer if were placed at a given menu item
			private function getArrowPositionAtIndex(index:Number):Number {
				var width:int = this.main.config.menuPointerSize;
				var selectedItem:IVisualElement = list.dataGroup.getElementAt(list.selectedIndex);
				var selectedItemX:Number = selectedItem.x;
				var selectedItemWidth:Number = selectedItem.width;
				var xPos:Number = (stage.stageWidth / 10) + selectedItemX + (selectedItemWidth / 2) - (width / 2);
				
				return xPos;
			}
			
		//Get information about the currently selected menu item by index
			public function getInfoForItemByIndex(currentlySelected:Boolean = true, index:int = 0):NavigationItemData {
				if (this.data && this.data != null) {
					var item:int;
					
					if (currentlySelected) {
						item = list.selectedIndex;
					} else {
						item = index;
					}
					
					if (item >= 0) {
						var info:NavigationItemData = new NavigationItemData();
						info.category = this.data.item[item].category;
						info.menuIndex = item;
						info.pageID = this.data.item[item].id;
						info.pageTitle = this.data.item[item].title;
						info.pageType = this.data.item[item].type;
						info.pageURL = this.data.item[item].URL;
						
						return info;
					} else {
						return new NavigationItemData();
					}
				}
				
				return new NavigationItemData();
			}
			
		//Get information about the currently selected menu item by URL
			public function getInfoForItemByURL(URL:String = ""):NavigationItemData {
				if (this.data && this.data != null) {
					var item:int;
					
				//Find the index of the item by its URL or just use the currently selected item
					if (URL != "") {
						for (var i:int = 0; i <= this.data.item.length - 1; i ++) {
							if (this.data.item[i].URL == this.URL) {
								item = i;
								break;
							}
						}
					} else {
						item = list.selectedIndex;
					}
					
				//Return data about the menu item
					if (item >= 0) {
						var info:NavigationItemData = new NavigationItemData();
						info.category = this.data.item[item].category;
						info.menuIndex = item;
						info.pageID = this.data.item[item].id;
						info.pageTitle = this.data.item[item].title;
						info.pageType = this.data.item[item].type;
						info.pageURL = this.data.item[item].URL;
						
						return info;
					} else {
						return new NavigationItemData();
					}
				}
				
				return new NavigationItemData();
			}
			
		//Let the application know that the user has selected a menu item
			private function menuItemSelectedHandler(e:IndexChangeEvent):void {
			//Collect the event data
				var event:MenuEvent = new MenuEvent("menuItemClicked");
				event.category = this.data.item[list.selectedIndex].category;
				event.menuIndex = list.selectedIndex;
				event.pageID = this.data.item[list.selectedIndex].id;
				event.pageTitle = this.data.item[list.selectedIndex].title;
				event.pageType = this.data.item[list.selectedIndex].type;
				event.pageURL = this.data.item[list.selectedIndex].URL;
				
			//Dispatch the event
				super.dispatchEvent(event);
			}
		]]>
	</fx:Script>
	
	<fx:Metadata>
		[Event(name="dataFetched", type="flash.events.Event")]
		[Event(name="menuConstructed", type="flash.events.Event")]
		[Event(name="menuItemClicked", type="com.forwardfour.events.MenuEvent")]
		[Event(name="renderComplete", type="flash.events.Event")]
	</fx:Metadata>
	
<!-- Make a request to the server for each of the menu items -->
	<fx:Declarations>
		<s:CallResponder fault="dataFaultHandler(event)" id="dataServiceResponder" result="dataResultHandler(event)"/>
		<s:HTTPService id="dataService" url="services/menu.php"/>
	</fx:Declarations>
	
<!-- The background of the menu -->
	<s:Rect height="1" horizontalCenter="0" id="background" width="0">
		<s:fill>
			<s:SolidColor color="0x333333"/>
		</s:fill>
	</s:Rect>
	
<!-- The styled list which will act as the menu -->
	<s:List borderVisible="false" change="menuItemSelectedHandler(event)" contentBackgroundAlpha="0"
			dataProvider="{this.data.item}" id="list" itemRenderer="renderers.MainMenuRenderer"
			left="{width * 0.1}" requireSelection="true" right="{width * 0.1}">
		<s:layout>
			<s:HorizontalLayout gap="30"/>
		</s:layout>
	</s:List>
</s:Group>