<?xml version="1.0" encoding="utf-8"?>
<s:Group xmlns:fx="http://ns.adobe.com/mxml/2009" 
		 xmlns:s="library://ns.adobe.com/flex/spark" 
		 xmlns:mx="library://ns.adobe.com/flex/mx"
		 width="100%"
		 creationComplete="init(event)">
	<fx:Script>
		<![CDATA[			
			import com.asual.swfaddress.SWFAddress;
			import com.forwardfour.events.MenuEvent;
			import com.forwardfour.exceptions.ServerDataGenerationError;
			import com.greensock.TweenMax;
			
			import mx.controls.Alert;
			import mx.core.FlexGlobals;
			import mx.core.IVisualElement;
			import mx.core.UIComponent;
			import mx.rpc.events.FaultEvent;
			import mx.rpc.events.ResultEvent;
			
			import spark.events.IndexChangeEvent;
			
		//Globalize a reference to the main application, so that the instantiated Config class can be accessed
			private var main:Main;
			
		//A reference to the data that was fetched from the server
			[Bindable]
			private var data:Object;
			
		//A reference to the current page pointer arrow
			private var pointer:UIComponent = null;
			
		//Request the menu data from the server, hide the menu scrollbars, and configure this component
			private function init(e:Event):void {
			//Request the menu data
				dataServiceResponder.token = dataService.send();
				
			//Hide menu scrollbars
				list.scroller.setStyle("horizontalScrollPolicy", "off");
				list.scroller.setStyle("verticalScrollPolicy", "off");
				
			//Configure the component
				this.configure();
			}
			
		//Use the configuration class to set the menu's construction properties and behaviors
			private function configure():void {
				this.main = Main(FlexGlobals.topLevelApplication);
				
				this.dataService.showBusyCursor = this.main.config.showFlexBusyCursor;
			}
			
		//Share the loaded data with the component, and transition the menu into view
			private function dataResultHandler(e:ResultEvent):void {
				try {
					if (e.result.menu && e.result.menu != null) {
						this.data = e.result.menu;
					} else {
						throw new ServerDataGenerationError("The server has returned invalid data while generating data for the Menu");
					}
				} catch (error:ServerDataGenerationError) {
					Alert.show(error.message);
				}
				
				this.slideOut();
			}
			
		//Handle data loading errors
			private function dataFaultHandler(e:FaultEvent):void {
				Alert.show("Data for the Menu could not be fetched.\n\nFault code: " +
							e.fault.faultCode + "\nFault details: " + e.fault.faultDetail, e.fault.faultString);
			}
			
		/**
		 * Perform the following actions to transitioning the menu into view
		 *  - [1] slide the out horizontally to full width
		 *  - [2] roll the menu down to full height
		 *  - [3] build the menu items (performed by each item renderer)
		 *  - [4] build the current page pointer arrow
		*/
			
			private function slideOut():void {
				TweenMax.to(this.background, (this.main.config.menuInitDuration / 1000) / 2, {
					onComplete : slideDown,
					width : stage.stageWidth
				});
			}
			
			private function slideDown():void {
				TweenMax.to(this.background, (this.main.config.menuInitDuration / 1000) / 2, {
					height : 40,
					onComplete : buildPointerDelay
				});
				
			//Anchor the backgroud of the menu to the left and right of the stage
				this.background.left = 0;
				this.background.right = -20;
			}
			
			private function buildPointerDelay():void {
				var delay:Number = this.main.config.menuInitDuration;                //Delay for the menu background
				delay += this.data.item.length * this.main.config.menuItemDuration;  //Delay for each menu item
				
				var timer:Timer = new Timer(delay, 1);
				timer.addEventListener(TimerEvent.TIMER_COMPLETE, buildPointer);
				timer.start();
			}
			
			private function buildPointer(e:TimerEvent):void {
			//Pointer configuration
				var color:uint = 0xFFFFFF;
				var width:int = this.main.config.menuPointerSize;
				
			//Building the pointer
				this.pointer = new UIComponent();
				this.pointer.alpha = 0;
				this.pointer.graphics.beginFill(color);
				this.pointer.graphics.moveTo(width / 2, 0);
				this.pointer.graphics.lineTo(width, width);
				this.pointer.graphics.lineTo(0, width);
				this.pointer.graphics.lineTo(width / 2, 0);
				this.pointer.graphics.endFill();
				this.pointer.y = 40 - width;
				this.addElement(this.pointer);
				
			//Place the arrow under the active menu item
				if (this.URL == "") {
					this.list.selectedIndex = 0;
					this.pointer.x = this.getArrowPositionAtIndex(0);
					this.selected = 0;
				} else {
					var menuData:Object = getInfoForItemByURL(this.URL);
					this.list.selectedIndex = menuData.menuIndex;
					this.pointer.x = this.getArrowPositionAtIndex(menuData.menuIndex);
					this.selected = menuData.menuIndex;
				}
			}
			
		//Get the x-axis position of the pointer if were placed at a given menu item
			private function getArrowPositionAtIndex(index:Number):Number {
				var width:int = this.main.config.menuPointerSize;
				var selectedItem:IVisualElement = list.dataGroup.getElementAt(list.selectedIndex);
				var selectedItemX:Number = selectedItem.x;
				var selectedItemWidth:Number = selectedItem.width;
				var xPos:Number = (stage.stageWidth / 10) + selectedItemX + (selectedItemWidth / 2) - (width / 2);
				
				return xPos;
			}
			
		//Set the selected menu item and slide the arrow to a new menu item
			public function set selected(index:Number):void {
			//Don't do anything if the pointer has not be created
				if (this.pointer != null) {
					list.selectedIndex = index;
					
					var xPos:Number = this.getArrowPositionAtIndex(list.selectedIndex);
					
					TweenMax.to(this.pointer, this.main.config.menuPointerTransitionDuration / 1000, {
						alpha : 1,
						x : xPos
					});
				}
			}
			
		//Get the selected menu item
			public function get selected():Number {
				return list.selectedIndex;
			}
			
		//Get information about the currently selected menu item by index
			public function getInfoForItemByIndex(currentlySelected:Boolean = true, index:int = 0):Object {
				if (this.data && this.data != null) {
					var item:int;
					
					if (currentlySelected) {
						item = list.selectedIndex;
					} else {
						item = index;
					}
					
					var info:Object = new Object();
					info.menuIndex = list.selectedIndex;
					info.pageID = this.data.item[item].id;
					info.pageTitle = this.data.item[item].title;
					info.pageType = this.data.item[item].type;
					info.pageURL = this.data.item[item].URL;
					
					return info;
				}
				
				return false;
			}
			
		//Get information about the currently selected menu item by URL
			public function getInfoForItemByURL(URL:String):Object {
				if (this.data && this.data != null) {
					var item:int = -1;
					
				//Find the index of the item by its URL
					for (var i:int = 0; i <= this.data.item.length - 1; i ++) {
						if (this.data.item[i].URL == this.URL) {
							item = i;
							break;
						}
					}
					
				//Return data about the menu item
					if (item == -1) {
						return new Object();
					}
					
					var info:Object = new Object();
					info.menuIndex = list.selectedIndex;
					info.pageID = this.data.item[item].id;
					info.pageTitle = this.data.item[item].title;
					info.pageType = this.data.item[item].type;
					info.pageURL = this.data.item[item].URL;
					
					return info;
				}
				
				return false;
			}
			
		//Let the application know that the user has selected a menu item
			private function menuItemSelectedHandler(e:IndexChangeEvent):void {
			//Collect the event data
				var event:MenuEvent = new MenuEvent("menuItemClicked");
				event.menuIndex = list.selectedIndex;
				event.pageID = this.data.item[list.selectedIndex].id;
				event.pageTitle = this.data.item[list.selectedIndex].title;
				event.pageType = this.data.item[list.selectedIndex].type;
				event.pageURL = this.data.item[list.selectedIndex].URL;
				
			//Dispatch the event
				super.dispatchEvent(event);
			}
			
		//Get the hijaxed URL of the current page
			private function get URL():String {
				var URL:String = SWFAddress.getValue();
				
				if (URL == "" || URL == "/") {
					return "";
				} else {
					return URL.substring(1);
				}
			}
		]]>
	</fx:Script>
	
	<fx:Metadata>
		[Event(name="menuItemClicked", type="com.forwardfour.events.MenuEvent")]
	</fx:Metadata>
	
<!-- Make a request to the server for each of the menu items -->
	<fx:Declarations>
		<s:CallResponder fault="dataFaultHandler(event)" id="dataServiceResponder" result="dataResultHandler(event)"/>
		<s:HTTPService id="dataService" url="services/menu.php"/>
	</fx:Declarations>
	
<!-- The background of the menu -->
	<s:Rect height="1" horizontalCenter="0" id="background" width="0">
		<s:fill>
			<s:SolidColor color="0x333333"/>
		</s:fill>
	</s:Rect>
	
<!-- The styled list which will act as the menu -->
	<s:List borderVisible="false" change="menuItemSelectedHandler(event)" contentBackgroundAlpha="0"
			dataProvider="{this.data.item}" id="list" itemRenderer="renderers.MainMenuRenderer"
			left="{width * 0.1}" requireSelection="true" right="{width * 0.1}">
		<s:layout>
			<s:HorizontalLayout gap="30"/>
		</s:layout>
	</s:List>
</s:Group>